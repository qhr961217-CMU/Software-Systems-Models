//
// 17-651: FSP model of a simple infusion pump
//
// ASSUMPTIONS
// - The user can choose, in the setup phase, which item of info to enter next.
// - The simple enter_value action is a simple number pad where the user enters
//   numeric values.
// - There exist 'set' and 'cancel' buttons, which are the targets of press_set
//   and press_cancel.
// - When the user turns the unit off, the unit will be in a state as if it was
//   just plugged in.
//

//
// Set of actions that the user of the LTSA tool can control in an
// animation of this model.
//
menu UserControlMenu = {
	// POWER_SYSTEM
	e_reconnect, plug_in, unplug, turn_off,

	// PUMP
    change_settings, clear_rate, confirm_settings, connect_set,
    dispense_main_med_flow, enter_value, erase_and_unlock_line,
    flow_unblocked, sound_alarm, lock_unit, press_cancel, 
    press_set, set_rate, silence_alarm, unlock_unit,
    flow_blocked
}

//======================
// Constants and Ranges
//======================

//
// States of the pump alarm
//
const AlarmActivated = 0    // Alarm currently active
const AlarmSilenced  = 1    // Alarm currently inactive

range AlarmStateT = AlarmActivated .. AlarmSilenced

//
// States of the pump settings
//
const ParamsNotSet = 2    // pump parameters not set yet
const ParamsSet    = 3    // pump parameters already set

range ParamsStateT = ParamsNotSet .. ParamsSet

//
// Locked/unlocked states of a line with respect to a pump channel
//
const LineUnlocked = 4  // line not locked into a pump channel 
const LineLocked   = 5  // line locked into a pump channel

range LineLockStateT = LineUnlocked .. LineLocked

//
// Locked/unlocked states of the pump unit
//
const UnitUnlocked = 6  // the keypad of the pump is not locked
const UnitLocked   = 7  // the keypad of the pump is locked

range UnitLockStateT = UnitUnlocked .. UnitLocked

//=====================
// Power System Process 
//=====================

const ElecUnplugged = 0
const ElecPlugged = 1

range ElecPlugStateT = ElecUnplugged .. ElecPlugged

POWER_SYSTEM = UNPLUGGED,
UNPLUGGED = (
	turn_on -> (
		plug_in -> PLUGGED
		|
		e_fail -> BATT_WORKING[ElecUnplugged]
	)
),
PLUGGED = (
	e_connect -> ELEC_WORKING
),
ELEC_WORKING = (
	e_fail -> BATT_WORKING[ElecPlugged]
	|
	turn_off -> UNPLUGGED
	|
	unplug -> BATT_WORKING[ElecUnplugged]
),
BATT_WORKING[isPlugged:ElecPlugStateT] = (
	e_reconnect -> ELEC_WORKING
	|
	b_fail -> P_FAILING[isPlugged]
	|
	turn_off -> UNPLUGGED
	|
	when (isPlugged == ElecPlugged)
		unplug -> BATT_WORKING[ElecUnplugged]
),
P_FAILING[isPlugged:ElecPlugStateT] = (
	when (isPlugged == ElecPlugged)
		e_reconnect -> turn_on -> ELEC_WORKING
	|
	when (isPlugged == ElecUnplugged)
		plug_in -> turn_on -> ELEC_WORKING
	|
	b_replace -> turn_on -> BATT_WORKING[isPlugged]
).

//=====================
// Line Process 
//=====================


//=====================
// Pump Process 
//=====================

PUMP = POWER_OFF,
POWER_OFF = 
(
    turn_on -> SETUP[ParamsNotSet][LineUnlocked]
),

//
// Pump in setup mode:
// - Once required pump parameters (just rate in this case) are set,
//   physical connections can be made and line can be locked
//
SETUP[params:ParamsStateT][lineLock:LineLockStateT] = 
(
	power_fail -> POWER_OFF
	|
    turn_off -> POWER_OFF
    |
    when (params == ParamsNotSet && lineLock == LineUnlocked)
        set_rate -> enter_value ->
            (press_set -> SETUP[ParamsSet][lineLock]
             |
             press_cancel -> SETUP[ParamsNotSet][lineLock])
    |
    when (params == ParamsSet && lineLock == LineUnlocked)
        clear_rate -> SETUP[ParamsNotSet][lineLock]
    |
    when (params == ParamsSet && lineLock == LineUnlocked)
        connect_set -> purge_air -> lock_line -> SETUP[params][LineLocked]
    |
    when (lineLock == LineLocked)
        confirm_settings -> INFUSION[UnitUnlocked]
    |
    when (lineLock == LineLocked)
        erase_and_unlock_line -> SETUP[params][LineUnlocked]
),

//
// Pump in infusion mode:
// - Always be able to turn the unit off, even if locked
// - Allow the user to lock/unlock the unit
// - Errors could occur with the pump (e.g., line became pinched or plugged)
//
INFUSION[unitLock:UnitLockStateT] =
(
	power_fail -> POWER_OFF
	|
    turn_off -> POWER_OFF
    |
    when (unitLock == UnitUnlocked)
        change_settings -> SETUP[ParamsSet][LineLocked]
    |
    when (unitLock == UnitUnlocked)
        lock_unit -> INFUSION[UnitLocked]
    |
    when (unitLock == UnitLocked)
        unlock_unit -> INFUSION[UnitUnlocked]
    |
    dispense_main_med_flow -> INFUSION[unitLock]
    |
    flow_blocked -> sound_alarm -> INFUSION_BLOCKED[unitLock][AlarmActivated]
),

//
// Pump stuck (medication flow suspended)
//
INFUSION_BLOCKED[unitLock:UnitLockStateT][alarm:AlarmStateT] =
(
	power_fail -> POWER_OFF
	|
    turn_off -> POWER_OFF
    |
    when (alarm == AlarmActivated)
        sound_alarm -> INFUSION_BLOCKED[unitLock][alarm]
    |
    when (alarm == AlarmActivated)
        silence_alarm -> INFUSION_BLOCKED[unitLock][AlarmSilenced]
    |
    flow_unblocked -> INFUSION[unitLock]
).


||INFUSSION_PUMP(N=2) = (p[1..N]:PUMP || {p[1..N]}::POWER_SYSTEM)
	/ {power_fail/p[1..N].b_fail, turn_on/p[1..N].turn_on}.
